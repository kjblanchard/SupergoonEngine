set(LIBRARY_TARGET_NAME SupergoonEngine)
message(STATUS "Starting ${LIBRARY_TARGET_NAME} engine initialization")
set(COMPILE_OPTIONS)
set(EXTERNAL_LIBS)
# Should we show everything, or just a few.
set(CMAKE_INSTALL_MESSAGE LAZY)
set(CMAKE_C_STANDARD 99)
set(CMAKE_C_STANDARD_REQUIRED ON)
set(BUILD_SHARED_LIBS
    OFF
    CACHE BOOL "Build libraries as static" FORCE)

# Build system Specific
if(CMAKE_GENERATOR STREQUAL "Xcode")
    message(STATUS "Setting Build to Universal Binary")
    set(CMAKE_OSX_ARCHITECTURES
        "x86_64;arm64"
        CACHE STRING "" FORCE)
endif(CMAKE_GENERATOR STREQUAL "Xcode")

if(DEBUG_ASAN AND CMAKE_BUILD_TYPE STREQUAL "Debug")
    if(NOT (APPLE AND (CMAKE_SYSTEM_NAME STREQUAL "iOS" OR CMAKE_SYSTEM_NAME STREQUAL "iossimulator")) AND NOT (CMAKE_SYSTEM_NAME STREQUAL "Emscripten"))
        message(STATUS "Enabling AddressSanitizer in Debug mode")
        set(ASAN_FLAGS "-fsanitize=address -fno-omit-frame-pointer")
        set(CMAKE_C_FLAGS "${CMAKE_C_FLAGS} ${ASAN_FLAGS}")
        set(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} ${ASAN_FLAGS}")
        set(CMAKE_EXE_LINKER_FLAGS "${CMAKE_EXE_LINKER_FLAGS} ${ASAN_FLAGS}")
        set(CMAKE_SHARED_LINKER_FLAGS "${CMAKE_SHARED_LINKER_FLAGS} ${ASAN_FLAGS}")

    endif()
endif()

# Platform specific executable flags
if(EMSCRIPTEN)
    message(STATUS "Enabling Emscripten flags for supergoon engine ")
    set(CMAKE_EXECUTABLE_SUFFIX
        ".html"
        CACHE STRING "Use html" FORCE)
    set(CMAKE_C_FLAGS "${CMAKE_C_FLAGS} -s USE_SDL=3 -s USE_ZLIB=1  -s USE_VORBIS=1 -s USE_OGG=1 ")
    set(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} -s USE_SDL=3 -s USE_ZLIB=1  -s USE_VORBIS=1 -s USE_OGG=1 ")
    set(LINK_FLAGS
        "${LINK_FLAGS} -s ALLOW_MEMORY_GROWTH=1 -s USE_SDL=3 -s USE_VORBIS=1 -s USE_OGG=1 -s FULL_ES3=1 -s USE_WEBGL2=1 -s STACK_SIZE=1mb -s EXPORTED_RUNTIME_METHODS=cwrap -sMAX_WEBGL_VERSION=2 -sMIN_WEBGL_VERSION=2 -s 'DEFAULT_LIBRARY_FUNCS_TO_INCLUDE=[\"$autoResumeAudioContext\",\"$dynCall\"]'"
    )
elseif(APPLE)
    set(CMAKE_OSX_DEPLOYMENT_TARGET
        "10.14"
        CACHE STRING "Minimum OS X deployment version")
    set(IS_BUNDLE
        "MACOSX_BUNDLE"
        CACHE STRING "Is this a bundle")
    set(ICON_PATH
        ${CMAKE_SOURCE_DIR}/cmake/AppIcon.icns
        CACHE STRING "Icon path")
endif(EMSCRIPTEN)

# Compile definitions based on backend
if(SDL_BACKEND)
    add_definitions(-Dsdlbackend)
endif(SDL_BACKEND)
if(SDL_GL)
    add_definitions(-Dsdlglbackend)
endif(SDL_GL)

if(UNIX OR APPLE)
    list(APPEND COMPILE_OPTIONS -Wall -Wextra -Wno-pedantic -Wdeprecated -Wno-unused-parameter -Wno-sign-compare)
endif(UNIX OR APPLE)

set(SRC_FILES
    src/camera.c
    src/lua.c
    src/log.c
    src/state.c
    src/Input/keyboard.c
    src/window.c
    src/filesystem.c
    src/tools.c
    src/events.c
    src/engine.c
    src/Primitives/rectangle.c
    src/Audio/Audio.c
    src/Tweening/easing.c
    # src/Tweening/tween.c
    src/Lua/llog.c
    src/Lua/laudio.c
    src/Lua/lengine.c
    src/Lua/linput.c
    src/Lua/lcamera.c
    src/Lua/scripting.c
    # src/effects.c
    src/Animation/Animator.c
    src/gameobject.c
    src/sprite.c
    src/Input/mouse.c
    # src/Input/touch.c
    src/Platform/sdl/sdl.c
    src/Platform/sdl/sdlInput.c
    src/Platform/sdl/sdlWindow.c
    src/map.c
    src/Lua/lanimation.c
    src/Lua/lsprite.c
    src/Lua/lgameobject.c
    src/Lua/lscene.c
    src/Lua/leffects.c
    src/Lua/lgraphics.c
    src/Lua/ltext.c
    # sound
    src/Platform/openal/openal.c
    src/Platform/openal/openalSfx.c
    src/Platform/openal/openalStream.c
    src/Platform/openal/openalBgm.c
    # 2d engine
    src/Graphics/graphics.c
    src/Graphics/texture.c
    src/Graphics/shader.c
    # input
    src/Input/joystick.c
    src/text.c)

if(SDL_GL)
    list(APPEND SRC_FILES src/Platform/opengl/glad.c src/Platform/opengl/openglGraphics.c src/Platform/opengl/openglShader.c src/Platform/opengl/openglTexture.c)
endif(SDL_GL)

# add_executable(${LIBRARY_TARGET_NAME } MACOSX_BUNDLE WIN32 ${SRC_FILES} ${DEBUG_FILES} ${ICON_PATH})
add_library(${LIBRARY_TARGET_NAME} ${SRC_FILES} ${DEBUG_FILES})

set(SKIP_INSTALL_ALL
    ON
    CACHE BOOL "We don't want any of these external packages to install anything")
set(CMAKE_DEBUG_POSTFIX
    ""
    CACHE STRING " Don't use the d for debug packages" FORCE)
set(CMAKE_DEBUG_TARGET_PROPERTIES
    "DEBUG_OUTPUT" ""
    CACHE STRING "Don't use the d for debug packages" FORCE)

if(APPLE)
    set_target_properties(${LIBRARY_TARGET_NAME} PROPERTIES MACOSX_BUNDLE_INFO_PLIST ${CMAKE_SOURCE_DIR}/cmake/Info.plist)
    set_source_files_properties(${ICON_PATH} PROPERTIES MACOSX_PACKAGE_LOCATION Resources)
    set_source_files_properties(${CMAKE_SOURCE_DIR}/cmake/Info.plist PROPERTIES MACOSX_PACKAGE_LOCATION Resources)
    set_target_properties(
        ${LIBRARY_TARGET_NAME}
        PROPERTIES XCODE_ATTRIBUTE_CODE_SIGN_ENTITLEMENTS "${CMAKE_SOURCE_DIR}/cmake/EscapeTheFate.entitlements"
        # XCODE_ATTRIBUTE_CODE_SIGN_STYLE "Automatic"
        XCODE_ATTRIBUTE_CODE_SIGN_STYLE "Manual"
        XCODE_ATTRIBUTE_DEVELOPMENT_TEAM "H34NGW287W"
        # when building on github, we don't want to sign anything cause there is no cert
        XCODE_ATTRIBUTE_CODE_SIGNING_REQUIRED "NO"
        XCODE_ATTRIBUTE_CODE_SIGNING_ALLOWED "NO"
        # XCODE_ATTRIBUTE_CODE_SIGN_IDENTITY "Apple Development"
    )

elseif(EMSCRIPTEN)
    if(EXISTS "${CMAKE_SOURCE_DIR}/SupergoonEngine/scripting")
        set(ENGINE_SCRIPT_PATH "${CMAKE_SOURCE_DIR}/SupergoonEngine/scripting")
    elseif(EXISTS "${CMAKE_BINARY_DIR}/_deps/supergoonengine-src/scripting")
        set(ENGINE_SCRIPT_PATH "${CMAKE_BINARY_DIR}/_deps/supergoonengine-src/scripting")
    else()
        message(WARNING "SupergoonEngine scripting folder not found, skipping preload.")
        set(ENGINE_SCRIPT_PATH "")
    endif()

    set_target_properties(
        ${LIBRARY_TARGET_NAME}
        PROPERTIES
        LINK_FLAGS
        "${LINK_FLAGS} -O0 -s SAFE_HEAP=2 -s STACK_OVERFLOW_CHECK=1 \
        --preload-file ${CMAKE_SOURCE_DIR}/assets@/assets \
        --preload-file ${CMAKE_SOURCE_DIR}/src@/assets/lua/ \
        --preload-file ${ENGINE_SCRIPT_PATH}@/assets/lua/")
    endif()

    if(SYSTEM_PACKAGES) # If we are using system packages, then we should try to find them.

        find_library(
            LIB_OPENAL
            PATH_SUFFIXES .dylib .a .dll
            NAMES libopenal.dylib libopenal openal libopenal.a openal.dll libopenal.a
            HINTS /usr/local/lib /c/cmake/lib)

        if(LIB_OPENAL)
            list(APPEND EXTERNAL_LIBS ${LIB_OPENAL})
        endif(LIB_OPENAL)

        find_library(
            LIB_CGLM
            PATH_SUFFIXES .a .dll
            NAMES libcglm cglm libcglm.a cglm.dll libcglm.a
            HINTS /usr/local/lib /c/cmake/lib)

        if(LIB_CGLM)
            list(APPEND EXTERNAL_LIBS ${LIB_CGLM})
        endif(LIB_CGLM)

        find_library(
            LIB_VORBIS
            PATH_SUFFIXES .a .dll
            NAMES libvorbis vorbis vorbis.a vorbis.dll libvorbis.a
            HINTS /usr/local/lib /c/cmake/lib)

        if(LIB_VORBIS)
            list(APPEND EXTERNAL_LIBS ${LIB_VORBIS})
        endif(LIB_VORBIS)

        find_library(
            LIB_VORBISFILE
            PATH_SUFFIXES .a .dll
            NAMES libvorbisfile vorbisfile vorbisfile.a vorbisfile.dll
            HINTS /usr/local/lib)

        if(LIB_VORBISFILE)
            list(APPEND EXTERNAL_LIBS ${LIB_VORBISFILE})
        endif(LIB_VORBISFILE)

        find_library(
            LIB_OGG
            PATH_SUFFIXES .a .dll
            NAMES ogg Ogg
            HINTS /usr/local/lib)

        if(LIB_OGG)
            list(APPEND EXTERNAL_LIBS ${LIB_OGG})
        endif(LIB_OGG)

        find_library(
            LIB_LUA
            NAMES lua lua_static lua5.4
            HINTS /opt/homebrew/bin/ /usr/local/bin /usr/local/lib /usr/bin/)

        if(LIB_LUA)
            list(APPEND EXTERNAL_LIBS ${LIB_LUA})
            target_include_directories(${LIBRARY_TARGET_NAME} PUBLIC /usr/include/lua5.4) #local debian install
        endif(LIB_LUA)
        find_package(SDL3 CONFIG COMPONENTS SDL3-shared)
        if(SDL3_FOUND)
            list(APPEND EXTERNAL_LIBS SDL3::SDL3)
        endif(SDL3_FOUND)
        find_package(Freetype)
        if(FREETYPE_FOUND)
            # list(APPEND EXTERNAL_LIBS ${FREETYPE_LIBRARY})
            target_include_directories(${LIBRARY_TARGET_NAME} PUBLIC /usr/local/include/freetype2 /opt/homebrew/opt/freetype/include/freetype2 /usr/include/freetype2)
            list(APPEND EXTERNAL_LIBS ${FREETYPE_LIBRARY})
        endif(FREETYPE_FOUND)

    endif(SYSTEM_PACKAGES)

    if(NOT LIB_CGLM)
        message(STATUS "CGLM not found. Fetching CGLM...")
        set(CGLM_SHARED
            OFF
            CACHE INTERNAL "No shared")
        set(CGLM_STATIC
            ON
            CACHE INTERNAL "yes")
        set(INSTALL_CMAKE_PACKAGE_MODULE
            OFF
            CACHE BOOL "Dont do it" FORCE)
        FetchContent_Declare(
            LIB_CGLM
            GIT_REPOSITORY https://github.com/recp/cglm
            GIT_TAG v0.9.6
            EXCLUDE_FROM_ALL)
        FetchContent_MakeAvailable(LIB_CGLM)
        target_link_libraries(${LIBRARY_TARGET_NAME} PUBLIC cglm)
    endif(NOT LIB_CGLM)

    if(NOT EMSCRIPTEN AND NOT LIB_OGG)
        message(STATUS "OGG not found. Fetching OGG...")
        set(BUILD_TESTING
            OFF
            CACHE BOOL "do not build tests")
        FetchContent_Declare(
            LIB_OGG
            GIT_REPOSITORY https://github.com/xiph/ogg
            GIT_TAG v1.3.5
            EXCLUDE_FROM_ALL)
        FetchContent_MakeAvailable(LIB_OGG)
        get_target_property(OGG_INCLUDE_DIR ogg INTERFACE_INCLUDE_DIRECTORIES)
        set(OGG_LIBRARY ogg)
        # list(APPEND EXTERNAL_LIBS ogg)
        target_link_libraries(${LIBRARY_TARGET_NAME} PUBLIC ogg)
    endif(NOT EMSCRIPTEN AND NOT LIB_OGG)

    if(NOT EMSCRIPTEN AND NOT LIB_VORBIS)
        message(STATUS "VORBIS not found. Fetching VORBIS...")
        set(INSTALL_CMAKE_PACKAGE_MODULE
            OFF
            CACHE BOOL "Dont do it" FORCE)
        FetchContent_Declare(
            LIB_VORBIS
            GIT_REPOSITORY https://github.com/xiph/vorbis
            GIT_TAG v1.3.7
            EXCLUDE_FROM_ALL)
        FetchContent_MakeAvailable(LIB_VORBIS)
        list(APPEND EXTERNAL_LIBS vorbisenc vorbisfile vorbis)
    endif(NOT EMSCRIPTEN AND NOT LIB_VORBIS)

    if(NOT LIB_LUA)
        message(STATUS "Lua not found. Fetching Lua...")
        set(LUA_ENABLE_TESTING
            OFF
            CACHE BOOL "Build SDL as a shared library")
        set(LUA_ENABLE_SHARED
            OFF
            CACHE BOOL "Build SDL as a shared library")
        set(LUA_BUILD_COMPILER
            OFF
            CACHE BOOL "Build SDL as a shared library")
        set(LUA_BUILD_BINARY
            OFF
            CACHE BOOL "Build SDL as a shared library")
        if(IOS) # Test for ios sim with lua, as this fails with system and needs this define.. TODO not sure if needed
            add_definitions(-DTARGET_OS_IOS=1)
            add_definitions(-DLUA_USE_IOS=1)
        endif(IOS)

        FetchContent_Declare(
            LIB_LUA
            GIT_REPOSITORY https://github.com/walterschell/Lua.git
            GIT_TAG v5.4.5
            EXCLUDE_FROM_ALL)
        FetchContent_MakeAvailable(LIB_LUA)

        # list(APPEND EXTERNAL_LIBS lua_static)
        list(APPEND EXTERNAL_LIBS lua_static)
    endif(NOT LIB_LUA)

    if(NOT EMSCRIPTEN AND NOT SDL3_FOUND)
        message(STATUS "SDL3 not found. Fetching SDL3...")
        set(SDL_SHARED
            OFF
            CACHE BOOL "Build SDL as a shared library")
        set(SDL_STATIC
            ON
            CACHE BOOL "Build SDL as a shared library")
        set(SDL_TEST
            OFF
            CACHE BOOL "Build SDL as a shared library")
        set(ENABLE_SDL_STATIC
            OFF
            CACHE BOOL "Build SDL as a static library")
        FetchContent_Declare(
            SDL3
            GIT_REPOSITORY https://github.com/libsdl-org/SDL
            GIT_TAG release-3.2.24
            EXCLUDE_FROM_ALL)
        FetchContent_MakeAvailable(SDL3)
        list(APPEND EXTERNAL_LIBS SDL3::SDL3-static)
    endif(NOT EMSCRIPTEN AND NOT SDL3_FOUND)

    if(NOT FREETYPE_FOUND)
        message(STATUS "freetype not found. Fetching freetype...")
        set(FT_DISABLE_BROTLI
            ON
            CACHE BOOL "Do not install when installing" FORCE)
        set(FT_DISABLE_HARFBUZZ
            ON
            CACHE BOOL "Do not install when installing" FORCE)
        set(FT_DISABLE_ZLIB
            ON
            CACHE BOOL "Do not use" FORCE)
        set(FT_DISABLE_BZIP2
            ON
            CACHE BOOL "Do not use" FORCE)
        set(FT_DISABLE_PNG
            ON
            CACHE BOOL "Do not use" FORCE)
        set(FT_DISABLE_PNG
            ON
            CACHE BOOL "Do not use" FORCE)
        FetchContent_Declare(
            LIB_FREETYPE
            GIT_REPOSITORY https://github.com/freetype/freetype
            GIT_TAG VER-2-13-3
            EXCLUDE_FROM_ALL)
        FetchContent_MakeAvailable(LIB_FREETYPE)
        list(APPEND EXTERNAL_LIBS freetype)
    endif(NOT FREETYPE_FOUND)

    if(NOT EMSCRIPTEN AND NOT LIB_OPENAL)
        message(STATUS "openal not found. Fetching openal...")
        set(BUILD_SHARED_LIBS
            OFF
            CACHE INTERNAL "Do not use")
        set(LIBTYPE
            "STATIC"
            CACHE INTERNAL "Do not use")
        set(ALSOFT_UTILS
            OFF
            CACHE INTERNAL "")
        set(ALSOFT_EXAMPLES
            OFF
            CACHE INTERNAL "")
        set(ALSOFT_BACKEND_WAVE
            OFF
            CACHE INTERNAL "")
        set(ALSOFT_EMBED_HRTF_DATA
            ON
            CACHE INTERNAL "")
        # set(ALSOFT_REQUIRE_COREAUDIO ON CACHE INTERNAL "" )
        FetchContent_Declare(
            LIB_OPENAL
            GIT_REPOSITORY https://github.com/kcat/openal-soft
            GIT_TAG 1.24.3
            EXCLUDE_FROM_ALL)
        FetchContent_MakeAvailable(LIB_OPENAL)
        target_link_libraries(${LIBRARY_TARGET_NAME} PUBLIC OpenAL)
    endif(NOT EMSCRIPTEN AND NOT LIB_OPENAL)

    if(DEBUG_CMAKE_VARIABLES)
        get_cmake_property(ALL_VARIABLES VARIABLES)
        foreach(VAR ${ALL_VARIABLES})
            message(STATUS "${VAR} = ${${VAR}}")
        endforeach()
    endif(DEBUG_CMAKE_VARIABLES)

    target_compile_options(${LIBRARY_TARGET_NAME} PUBLIC ${COMPILE_OPTIONS})
    target_link_directories(${LIBRARY_TARGET_NAME} PUBLIC /usr/local/lib) # should always include from usr/local/lib
    target_link_libraries(${LIBRARY_TARGET_NAME} PUBLIC ${EXTERNAL_LIBS})

    if(LINK_M)
        target_link_libraries(${LIBRARY_TARGET_NAME} PUBLIC m)
    endif(LINK_M)

    # System is for blocking out the warnings, these are mostly external libs
    target_include_directories(
        ${LIBRARY_TARGET_NAME} SYSTEM
        PUBLIC ${CMAKE_CURRENT_LIST_DIR}/include /usr/local/include
        PRIVATE # linux builds
        ${sdl3_SOURCE_DIR}/include ${freetype_SOURCE_DIR}/include ${lib_lua_SOURCE_DIR}/lua-5.4.5/include ${lib_vorbis_SOURCE_DIR}/include
        ${lib_ogg_SOURCE_DIR}/include ${lib_ogg_BINARY_DIR}/include)

    target_include_directories(${LIBRARY_TARGET_NAME} PRIVATE ${CMAKE_CURRENT_SOURCE_DIR}/include)
    target_include_directories(
        ${LIBRARY_TARGET_NAME} PRIVATE ${CMAKE_SOURCE_DIR}/src/engine/include ${CMAKE_CURRENT_SOURCE_DIR}/include ${CMAKE_CURRENT_SOURCE_DIR}_
        /emsdk/upstream/emscripten/cache/sysroot/include /usr/local/include/freetype2) # Add the freetype2 specific include
    # directory

    install(
        DIRECTORY scripting/
        DESTINATION ${INSTALL_DIR}/assets/lua
        COMPONENT assets)
